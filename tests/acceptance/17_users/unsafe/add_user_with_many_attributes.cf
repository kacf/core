#######################################################
#
# Test adding a user with many attributes.
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
}

#######################################################

bundle agent init
{
  # Remove him first, should he already be present.
  users:
    "johndoe"
      policy => "absent";
}

#######################################################

bundle agent test
{
  vars:
    "desc_string" string => "This description should make the CFEngine test pass";

  users:
    "johndoe"
      policy => "present",
      uid => "9876",
      # "2" is "bin" on most systems.
      group_primary => "2",
      groups_secondary => { "sys" },
      home_dir => "/user_directories/johndoe",
      shell => "$(G.echo)",
      description => "$(desc_string)";
}

#######################################################

bundle agent check
{
  classes:
    "passwd_ok" not => strcmp("0", countlinesmatching("johndoe:[^:]*:9876:2:$(test.desc_string):/user_directories/johndoe:$(G.echo)", "/etc/passwd"));
    "group_ok" not => strcmp("0", countlinesmatching("sys:[^:]*:[^:]*:[^:]*johndoe.*", "/etc/group"));
    "ok" and => { "passwd_ok", "group_ok" };

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
