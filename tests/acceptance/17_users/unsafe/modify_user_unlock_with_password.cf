#######################################################
#
# Test unlocking a user and setting a password.
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
}

#######################################################

bundle agent init
{
  users:
    "johndoe"
      policy => "locked";
}

#######################################################

bundle agent test
{
  vars:
    "hash" string => "$1$vUsR5eoG$cci6C2jKvoYNuyjL5macS1";

  users:
    "johndoe"
      policy => "present",
      password => test_password;
}

body password test_password
{
  format => "hash";
  data => "$(test.hash)";
}

#######################################################

bundle agent check
{
  vars:
    "hash" string => escape("$(test.hash)");

  classes:
    "ok_passwd" not => strcmp("0", countlinesmatching("johndoe:[^:]*:[^:]*:[^:]*:[^:]*:.*", "/etc/passwd"));
    "ok_shadow" not => strcmp("0", countlinesmatching("johndoe:$(hash):[^:]*:[^:]*:[^:]*:[^:]*:[^:]*::.*", "/etc/shadow"));

    "ok" and => { "ok_passwd", "ok_shadow" };

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
