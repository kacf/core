#######################################################
#
# Test modifying a user with many attributes.
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
}

#######################################################

bundle agent init
{
  # Start out with other attributes.
  users:
    "johndoe"
      policy => "present",
      uid => "8765",
      gid_primary => "3",
      gid_secondary => { getgid("bin") },
      shell => "$(G.cat)",
      description => "Wrong!";
}

#######################################################

bundle agent test
{
  vars:
    "desc_string" string => "This description should make the CFEngine test pass";

  users:
    "johndoe"
      policy => "present",
      uid => "9876",
      # "2" is "bin" on most systems.
      gid_primary => "2",
      gid_secondary => { getgid("sys") },
      shell => "$(G.echo)",
      description => "$(desc_string)";
}

#######################################################

bundle agent check
{
  classes:
    "ok" and => { !strcmp("0", countlinesmatching("johndoe:[^:]*:9876:2:$(test.desc_string):$(G.echo)", "/etc/passwd")),
                  !strcmp("0", countlinesmatching("sys:[^:]*:[^:]*:[^:]*johndoe.*", "/etc/group"))
                  strcmp("0", countlinesmatching("bin:[^:]*:[^:]*:[^:]*johndoe.*", "/etc/group"))
                };

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
